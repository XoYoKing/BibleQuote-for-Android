/*
 * Copyright (C) 2011 Scripture Software
 *
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 *
 * Project: BibleQuote-for-Android
 * File: build.gradle
 *
 * Created by Vladimir Yakushev at 3/2017
 * E-mail: ru.phoenix@gmail.com
 * WWW: http://www.scripturesoftware.org
 */

apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply from: '../jacoco.gradle'

android {
    compileSdkVersion 22
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.BibleQuote"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 82
        versionName "1.0.82"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                if (variant.buildType.name == "release") {
                    def apk = output.outputFile;
                    def newName = apk.name.replace(".apk", "-${defaultConfig.versionName}.apk");
                    output.outputFile = new File(apk.parentFile, newName);
                }
            }
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {

    // Android support libraries

    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:gridlayout-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:support-annotations:${rootProject.ext.supportLibVersion}"

    // Analytics

    compile("com.google.android.gms:play-services-analytics:8.4.0") {
        exclude group: "com.android.support", module: "support-v4"
    }

    // ButterKnife

    compile("com.jakewharton:butterknife:${rootProject.ext.butterknifeVersion}") {
        exclude group: "com.android.support", module: "support-annotations"
    }
    annotationProcessor "com.jakewharton:butterknife-compiler:${rootProject.ext.butterknifeVersion}"

    // Dagger 2

    compile "com.google.dagger:dagger:${rootProject.ext.daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"

    // Retrofit

    compile "com.squareup.retrofit2:retrofit:${rootProject.ext.retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${rootProject.ext.retrofitVersion}"

    // Others

    compile "com.github.danielnilsson9:color-picker-view:1.4.0@aar"

    // Test dependencies

    testCompile "junit:junit:4.12"
    testCompile "org.robolectric:robolectric:3.1.1"
    testCompile "org.mockito:mockito-core:2.2.27"
}
